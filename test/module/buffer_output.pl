#============================================================================================================
#
#	出力管理モジュール
#
#============================================================================================================
package	BUFFER_OUTPUT;

use strict;
use utf8;
use open IO => ':encoding(cp932)';
binmode(STDOUT,':encoding(cp932)');
use Encode;
use warnings;

#------------------------------------------------------------------------------------------------------------
#
#	モジュールコンストラクタ - new
#	-------------------------------------------
#	引　数：なし
#	戻り値：モジュールオブジェクト
#
#------------------------------------------------------------------------------------------------------------
sub new
{
	my $class = shift;
	
	my $obj = {
		'BUFF'	=> [],
	};
	bless $obj, $class;
	
	return $obj;
}

#------------------------------------------------------------------------------------------------------------
#
#	バッファ出力 - Print
#	-------------------------------------------
#	引　数：$line : 出力テキスト
#	戻り値：なし
#
#------------------------------------------------------------------------------------------------------------
sub Print
{
	my $this = shift;
	my ($line) = @_;

	push @{$this->{'BUFF'}}, $line;
}

#------------------------------------------------------------------------------------------------------------
#
#	INPUTタグ出力 - HTMLInput
#	-------------------------------------------
#	引　数：$kind  : タイプ
#			$name  : 名前
#			$value : 値
#	戻り値：なし
#
#------------------------------------------------------------------------------------------------------------
sub HTMLInput
{
	my $this = shift;
	my ($kind, $name, $value) = @_;
	
	my $line = "<input type=$kind name=\"$name\" value=\"$value\">\n";
	
	push @{$this->{'BUFF'}}, $line;
}

#------------------------------------------------------------------------------------------------------------
#
#	バッファフラッシュ - Flush
#	-------------------------------------------
#	引　数：$flag       : 出力フラグ
#			$perm		: パーミッション
#			$szFilePath : 出力パス
#	戻り値：なし
#
#------------------------------------------------------------------------------------------------------------
sub Flush
{
	my $this = shift;
	my ($flag, $perm, $path) = @_;
	
	# ファイルへ出力
	if ($flag) {
		chmod($perm, $path);
		if (open(my $fh, (-f $path ? '+<' : '>'), $path)) {
			flock($fh, 2);
			seek($fh, 0, 0);
			print $fh @{$this->{'BUFF'}};
			truncate($fh, tell($fh));
			close($fh);
		}
		chmod($perm, $path);
	}
	# 標準出力に出力
	else {
		if (exists $ENV{'FCGI_ROLE'}) {		# for nginx, if(grep {$_ eq 'FCGI.pm'} keys %INC){
			my $enc = Encode::find_encoding('cp932');
			foreach (@{$this->{'BUFF'}}) {
				print $enc->encode($_);
			}
		} else {
			print @{$this->{'BUFF'}};
		}
	}
}

#------------------------------------------------------------------------------------------------------------
#
#	バッファクリア - Clear
#	-------------------------------------------
#	引　数：なし
#	戻り値：なし
#
#------------------------------------------------------------------------------------------------------------
sub Clear
{
	my $this = shift;
	
	$this->{'BUFF'} = [];
}

#------------------------------------------------------------------------------------------------------------
#
#	マージ - Merge
#	-------------------------------------------
#	引　数：$buffer : BUFFER_OUTPUTモジュール
#	戻り値：なし
#
#------------------------------------------------------------------------------------------------------------
sub Merge
{
	my $this = shift;
	my ($buffer) = @_;
	
	push @{$this->{'BUFF'}}, @{$buffer->{'BUFF'}};
}

#============================================================================================================
#	モジュール終端
#============================================================================================================
1;
